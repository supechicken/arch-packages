pkgname=cage-custom
pkgver=0.2.0
pkgrel=3
pkgdesc="A kiosk compositor for Wayland"
arch=(x86_64)
url="https://www.hjdskes.nl/projects/cage/"
license=(MIT)
depends=(
  'glibc'
  'wayland'
  'wlroots0.19'
)
makedepends=(
  'git'
  'libxkbcommon'
  'meson'
  'scdoc'
  'wayland-protocols'
  'xorg-server-xwayland'
)
provides=('wayland-compositor')
optdepends=(
  'polkit: System privilege control. Required if not using seatd service'
  'xorg-server-xwayland: X11 support'
)
# It's not uncommon to need to update to the latest commit so we use git-based
# fetching.
source=("git+https://github.com/supechicken/cage.git")
install=cage.install
sha512sums=('SKIP')

build() {
  arch-meson cage build
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  depends+=(libwlroots-0.19.so libxkbcommon.so)

  DESTDIR="${pkgdir}" ninja -C build install
  install -vDm 644 "cage/README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "cage/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
