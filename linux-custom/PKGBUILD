pkgname=linux-custom
pkgver=6.6.65
pkgrel=1
arch=(x86_64)
license=(GPL-2.0-only)
provides=(linux-lts linux-lts-headers linux-api-headers)
conflicts=(linux-lts linux-lts-headers linux-api-headers)

makedepends=(
  llvm
  clang
  lld
  bc
  cpio
  gettext
  libelf
  pahole
  perl
  python
  tar
  xz
  zstd
  rsync
)

options=(!strip)

source=(
  https://github.com/intel/linux-intel-lts/archive/refs/tags/lts-v6.6.65-linux-241218T004222Z.tar.gz
  https://github.com/openwrt/openwrt/archive/refs/tags/v24.10.0-rc4.tar.gz
  https://gitlab.archlinux.org/archlinux/packaging/packages/linux-lts/-/archive/6.6.65-2/linux-lts-6.6.65-2.tar.gz
  https://gitlab.com/xanmod/linux-patches/-/archive/72c888e879f321c68e1e1a91d1ef9e5996fe518a/linux-patches-72c888e879f321c68e1e1a91d1ef9e5996fe518a.tar.gz
  config  # the main kernel config file
)

sha256sums=(SKIP SKIP SKIP SKIP SKIP)

_patches=(
  ../linux-lts-*/*.patch # arch patches
  ../linux-patches-*/linux-6.6.y-xanmod/net/tcp/{bbr3,cloudflare}/*.patch # network patches
  ../openwrt-*/target/linux/generic/{backport,hack,pending}-6.6/*.patch # openwrt patches
)

export SRCNAME=linux-intel-lts-lts-*/
export KBUILD_BUILD_HOST=Jumper-MiniPC
export KBUILD_BUILD_USER=supechicken

prepare() {
  cd $SRCNAME

  for p in ${_patches}; do
    echo "Applying patch $p..."
    patch -Np1 < $p
  done

  echo "Setting config..."
  cp ../config .config
  make ARCH=x86_64 LLVM=1 LLVM_IAS=1 olddefconfig
  diff -Nur --color ../config .config || :

  make -s kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"
}

build() {
  cd $SRCNAME

  make ARCH=x86_64 LLVM=1 LLVM_IAS=1 bzImage -j$(nproc)
  make ARCH=x86_64 LLVM=1 LLVM_IAS=1 modules -j$(nproc)
}

package() {
  mkdir -p $pkgdir/{boot,usr}
  cd $SRCNAME

  local modulesdir="$pkgdir/usr/lib/modules/$(<version)"

  make ARCH=x86_64 LLVM=1 LLVM_IAS=1 INSTALL_HDR_PATH=$pkgdir/usr headers_install -j$(nproc)

  ZSTD_CLEVEL=19 make \
    ARCH=x86_64 LLVM=1 LLVM_IAS=1 \
    DEPMOD=/doesnt/exist \
    INSTALL_MOD_PATH="$pkgdir/usr" \
    INSTALL_MOD_STRIP=1 \
    modules_install -j$(nproc)

  echo "Removing documentation..."
  rm -r Documentation

  echo "Removing loose objects..."
  find . -type f -name '*.o' -printf 'Removing %P\n' -delete

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  mkdir -p $pkgdir/usr/src
  cp -r . $pkgdir/usr/src/$pkgbase

  ln -sf /usr/src/$pkgbase $modulesdir/build
  install -Dm644 arch/x86/boot/bzImage $pkgdir/boot/vmlinuz-custom
}
